apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.0'
    defaultConfig {
        applicationId "com.zeapo.pwdstore"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 101
        versionName "2.0.0-dev"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        incremental true
    }

    // avoid Travis failures
    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude '.readme'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    //
    // To sign release builds, create the file `gradle.properties` in
    // $HOME/.gradle or in your project directory with this content:
    //
    // mStoreFile=/path/to/key.store
    // mStorePassword=xxx
    // mKeyAlias=alias
    // mKeyPassword=xxx
    //
    if (project.hasProperty('mStoreFile') &&
            project.hasProperty('mStorePassword') &&
            project.hasProperty('mKeyAlias') &&
            project.hasProperty('mKeyPassword')) {
        signingConfigs {
            release {
                storeFile file(mStoreFile)
                    storePassword mStorePassword
                    keyAlias mKeyAlias
                    keyPassword mKeyPassword
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }
}

ext.support_version = '24.0.0'

dependencies {
    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:recyclerview-v7:$support_version"
    compile "com.android.support:cardview-v7:$support_version"
    compile "com.android.support:design:$support_version"
    compile 'org.sufficientlysecure:openpgp-api:11.0'
    compile 'com.nononsenseapps:filepicker:2.4.2'
    compile ('org.eclipse.jgit:org.eclipse.jgit:3.7.+') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    compile 'com.jcraft:jsch:0.1.53'
    compile 'org.apache.commons:commons-io:1.3.2'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.3.1'
}
